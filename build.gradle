import static org.gradle.internal.os.OperatingSystem.*

plugins {
    id 'maven'
    id 'java'
    id "org.jetbrains.kotlin.jvm" version "1.3.41"
}

ext{
    kotlin = 'org.jetbrains.kotlin:kotlin'
    kx = 'com.github.kotlin-graphics'
    kotlin_version = '1.3.41'
    kotlintest_version = '3.3.3'
    lwjgl_version = "3.2.2"
    lwjgl_natives = current() == WINDOWS ? "windows" : current() == LINUX ? "linux" : "macos"
}

dependencies {

    implementation "$kotlin-stdlib"

    implementation "$kx:uno-sdk:cb9073be566b5ca09e3a6d2a8edfb661a721c751"
    implementation "$kx:gln:9e1493d800519e4f1df62e50e79ac99d2d5ad4ec"
    implementation "$kx:gli:c97a53f7ff98d276e4201905b7283e9893de5eeb"
    implementation "${kx}.glm:glm:6048c31425ae6110258e4b42165f1e636f8b5603"
    implementation "$kx:kool:d052c5e18a06642fcdf9f5942198ee9c4cee1a3f"
    implementation "$kx:kotlin-unsigned:ba1b5c2ab0231aabbef753df5fa0d0de98142360"

    testImplementation "io.kotlintest:kotlintest-runner-junit5:$kotlintest_version".toString()

    ["", "-glfw", "-jemalloc", "-openal", "-opengl", "-opengles", "-stb"].each {
        String base = "org.lwjgl:lwjgl$it:$lwjgl_version"
        implementation base
        runtime "$base:natives-$lwjgl_natives"
    }
}

test.useJUnitPlatform()

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
    }
//    destinationDir = compileJava.destinationDir
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
//    destinationDir = compileTestJava.destinationDir
}

repositories {
    mavenCentral()
    maven { setUrl("https://oss.sonatype.org/content/repositories/snapshots/") }
    maven { url 'https://jitpack.io' }
}